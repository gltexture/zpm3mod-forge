import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

buildscript {
    repositories {
        mavenCentral()
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        maven { url = 'https://maven.parchmentmc.org' }
        mavenLocal()
        flatDir {
            dirs 'libs'
        }
    }
    dependencies {
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
        classpath 'org.parchmentmc:librarian:1.+'
    }
}

plugins {
    id 'eclipse'
    id 'idea'
    id 'net.minecraftforge.gradle' version '[6.0.16,6.2)'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}
tasks.register('zpShadow', ShadowJar) {
    archiveClassifier.set('prod')
    from sourceSets.main.output
    from {
        project.configurations.minecraftLibrary.findAll {
            it.name.contains('imgui-java-binding') || it.name.contains('imgui-java-lwjgl3')
        }.collect { it.isDirectory() ? it : zipTree(it) }
    }
    from {
        project.configurations.minecraftLibrary.findAll {
            it.name.contains('imgui-java-natives-')
        }.collect { it.isDirectory() ? it : zipTree(it) }
    }
    manifest {
        attributes(["Specification-Title"     : mod_id,
                    "Specification-Vendor"    : mod_authors,
                    "Specification-Version"   : "1",
                    "Implementation-Title"    : project.name,
                    "Implementation-Version"  : project.jar.archiveVersion,
                    "Implementation-Vendor"   : mod_authors,
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")])
    }
}
apply plugin: 'org.spongepowered.mixin'

group = mod_group_id
version = mod_version

base {
    archivesName = mod_id
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
}

minecraft {
    mappings channel: mapping_channel, version: mapping_version
    copyIdeResources = true
    runs {
        configureEach {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        client {
            property 'forge.enabledGameTestNamespaces', mod_id
            workingDirectory project.file("run")
            args '--username', 'Dev'
        }

        server {
            property 'forge.enabledGameTestNamespaces', mod_id
            args '--nogui'
        }

        gameTestServer {
            property 'forge.enabledGameTestNamespaces', mod_id
        }

        data {
            workingDirectory project.file('run-data')
            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
}

mixin {
    add sourceSets.main, "${mod_id}.refmap.json"
    config "zpm3.guns.mixins.json"
    config "zpm3.main.mixins.json"
    config "zpm3.common.mixins.json"
    config "zpm3.mob_effects.mixins.json"
    config "zpm3.entities.mixins.json"
    config "zpm3.player.mixins.json"
    config "zpm3.debug.mixins.json"
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    implementation 'io.github.spair:imgui-java-binding:1.86.12'
    implementation 'io.github.spair:imgui-java-lwjgl3:1.86.12'
    implementation 'io.github.spair:imgui-java-natives-windows:1.86.12'
    implementation 'io.github.spair:imgui-java-natives-linux:1.86.12'
    implementation 'io.github.spair:imgui-java-natives-macos:1.86.12'

    minecraftLibrary 'io.github.spair:imgui-java-binding:1.86.12'
    minecraftLibrary 'io.github.spair:imgui-java-lwjgl3:1.86.12'
    minecraftLibrary 'io.github.spair:imgui-java-natives-windows:1.86.12'
    minecraftLibrary 'io.github.spair:imgui-java-natives-linux:1.86.12'
    minecraftLibrary 'io.github.spair:imgui-java-natives-macos:1.86.12'
}

tasks.named('processResources', ProcessResources).configure {
    var replaceProperties = [minecraft_version   : minecraft_version, minecraft_version_range: minecraft_version_range,
                             forge_version       : forge_version, forge_version_range: forge_version_range,
                             loader_version_range: loader_version_range,
                             mod_id              : mod_id, mod_name: mod_name, mod_license: mod_license, mod_version: mod_version,
                             mod_authors         : mod_authors, mod_description: mod_description,]

    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

tasks.named('jar', Jar).configure {
    from sourceSets.main.output
    from {
        project.configurations.minecraftLibrary.findAll {
            it.name.contains('imgui-java-')
        }.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes(["Specification-Title"     : mod_id,
                    "Specification-Vendor"    : mod_authors,
                    "Specification-Version"   : "1",
                    "Implementation-Title"    : project.name,
                    "Implementation-Version"  : project.jar.archiveVersion,
                    "Implementation-Vendor"   : mod_authors,
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")])
    }

    finalizedBy 'reobfJar'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.matching { it.name == "runData" }.configureEach {
    systemProperty "zpm3.datagen", "true"
}